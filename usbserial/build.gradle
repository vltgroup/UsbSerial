apply plugin: 'com.android.library'

android {

    group = 'com.felhr.usbserial'
    version = '1.0.0'

    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
    }

    //noinspection GroovyAssignabilityCheck
    task deleteJar(type: org.gradle.api.tasks.Delete){
        delete 'eclipse_lib/usbserial.jar'
    }

    //noinspection GroovyAssignabilityCheck
    task createJar(type: org.gradle.api.tasks.Copy){
        from('build/intermediates/bundles/release/')
        into('eclipse_lib/')
        include('classes.jar')
        rename('classes.jar', 'usbserial.jar')
    }

    createJar.dependsOn(deleteJar, build)
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
